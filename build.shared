apply plugin: 'java'
apply plugin: 'checkstyle'

repositories {
  mavenCentral()
  maven {
    url "http://oss.sonatype.org/content/groups/public/"
  }
}

checkstyle {
  configFile = "${rootDir}/config/checkstyle/checkstyle.xml" as File
}


dependencies {
  testCompile group: 'junit', name: 'junit', version: '4.12'

  compile (
    "org.threadly:threadly:$threadlyVersion",
    "org.threadly:litesockets:$litesocketsVersion",
  )
}

compileJava {
  options.compilerArgs << "-Xlint:all" << "-Werror"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all" << "-Xlint:-deprecation" << "-Werror"
}

plugins.withType(JavaPlugin) {
  checkstyle.sourceSets = [sourceSets.main]
}

test {
  getReports().getJunitXml().setDestination(file("${buildDir}/reports/tests/xml"))
  getReports().getHtml().setDestination(file("${buildDir}/reports/tests/html"))
  setBinResultsDir(file("${buildDir}/reports/tests/bin"))
}

jar {
  manifest {
    attributes 'Implementation-Title': 'litesockets', 'Implementation-Version': version
  }
}

javadoc {
  source = sourceSets.main.allJava
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}
